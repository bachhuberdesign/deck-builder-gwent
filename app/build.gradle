apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.bachhuberdesign.gwentcardviewer"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        incremental = true
    }

    dexOptions {
        javaMaxHeapSize "4096M"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    def support_version = "25.3.1"
    def conductor_version = "2.1.2"
    def dagger_version = "2.10"
    def retrofit_version = "2.2.0"
    def leakcanary_version = "1.5"

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // UI Testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
        exclude group: 'javax.inject'
    })

    // Unit Testing
    testCompile 'junit:junit:4.12'

    // Support Library
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "com.android.support:recyclerview-v7:$support_version"

    // Leak Canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Conductor
    compile "com.bluelinelabs:conductor:$conductor_version"
    compile "com.bluelinelabs:conductor-support:$conductor_version"
    compile "com.bluelinelabs:conductor-rxlifecycle2:$conductor_version"

    // Rx
    compile "io.reactivex.rxjava2:rxjava:2.0.7"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Networking
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // Gson
    compile "com.google.code.gson:gson:2.8.0"

    // Image Loading
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    // Dagger
    compile "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Database
    compile 'com.squareup.sqlbrite:sqlbrite:1.1.1'

    // FastAdapter
    compile('com.mikepenz:fastadapter:2.5.1@aar') { transitive = true }
    compile 'com.mikepenz:fastadapter-commons:2.5.0@aar'
    compile "com.mikepenz:materialize:1.0.1@aar"

    // Dialog
    compile 'com.afollestad.material-dialogs:core:0.9.4.2'
}

repositories {
    jcenter()
    maven {
        url 'https://dl.bintray.com/kotlin/kotlin-eap-1.1/'
    }
}

// Remove once MaterialDialogs has been updated to use latest support library
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.3.1'
            }
        }
    }
}